buildscript {
	ext {
		springBootVersion = '2.0.5.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.19.2')
	}

}

plugins {
    id 'com.palantir.docker-run' version '0.19.2'
    id 'com.palantir.docker' version '0.19.2'
    id "org.flywaydb.flyway" version "5.0.7"

}

flyway {
    driver = 'org.postgresql.Driver'
    url = 'jdbc:postgresql://localhost:5432/fbc_media_data'
    user = 'fbc_svc_user'
    password = 'devpassword'
    schemas = ['fbc_media'] // this does not affect server startup; set this in application.yml also
    skipDefaultCallbacks = false
    cleanDisabled = false
}


apply plugin: "com.palantir.docker-run"
apply plugin: "com.palantir.docker"
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


group = 'com.mattmartin.faithbible'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.228'
    }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-hateoas'){
        exclude module: "spring-boot-starter-logging"
        exclude module: "logback-classic"
    }
    compile('org.springframework.boot:spring-boot-starter-log4j2')
	compile('com.github.vanroy:spring-boot-starter-data-jest:3.1.2.RELEASE')
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8")
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.6" // for serialization of LocalDateTime in Java 8
    compile('io.springfox:springfox-swagger2:2.8.0')
    compile("io.springfox:springfox-swagger-ui:2.0.2")
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.flywaydb:flyway-core:5.0.7")
    compile("commons-net:commons-net:3.3")
    compile("com.mpatric:mp3agic:0.9.1")
    compile("commons-lang:commons-lang:2.6")
    compile("javax.xml.bind:jaxb-api:2.3.0")
    compile 'com.amazonaws:aws-java-sdk-s3'
	testCompile('org.springframework.boot:spring-boot-starter-test')

    runtime('org.postgresql:postgresql')
}
tasks.findByName('dockerRemoveContainer').dependsOn('dockerStop')
tasks.findByName('dockerRun').dependsOn('dockerRemoveContainer')
//tasks.findByName('test').dependsOn('dockerRun')
task runLocal(dependsOn: 'dockerRun'){
    doLast {
        println 'launching docker... be patient'
        sleep(15000)
    }
}

runLocal.finalizedBy('bootRun')

docker {
    dependsOn(build)
    name "${project.group}/${bootJar.baseName}"
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}

test {
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude module: "logback-classic"

    dockerRun {
        name 'elasticsearch'
        image 'docker.elastic.co/elasticsearch/elasticsearch:6.2.4'
        ports '9200:9200', '9300:9300'
        daemonize true
        env 'discovery.type': 'single-node'
    }



}
compileJava {
    options.fork = true
    options.forkOptions.executable = project.property('javacPath')
}